<?xml version="1.0"?>
<!DOCTYPE xsl:stylesheet [
  <!ENTITY tw "http://tw.rpi.edu/schema/">
]>
<xsl:stylesheet version="1.0"
		xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
		xmlns:rdf="http://www.w3.org/1999/02/22-rdf-syntax-ns#"
		xmlns:rdfs="http://www.w3.org/2000/01/rdf-schema#"
		xmlns:tw="&tw;"
		xmlns:time="http://www.w3.org/2006/time#"
		xmlns:owl="http://www.w3.org/2002/07/owl#"
		xmlns:s="http://www.w3.org/2005/sparql-results#"
		xmlns:exsl="http://exslt.org/common"
		xmlns="http://www.w3.org/1999/xhtml"
>
  <xsl:import href="/xslt/person-common.xsl"/>
  <xsl:import href="/xslt/theme-common.xsl"/>
  <xsl:import href="/xslt/image-common.xsl"/>
  <xsl:import href="/xslt/concept-common.xsl"/>
  <xsl:template name="section-theme-header">
    <xsl:param name="node"/>
    <xsl:param name="admin"/>
    <xsl:param name="embed"/>
    <xsl:param name="root" select="/"/>
    <xsl:param name="header" select="true()"/>

    <div class="theme">
      <link href="/css/theme.css" type="text/css" rel="stylesheet"/>
    <div class="header">
    <h1 xmlns:tw="&tw;">
      <xsl:if test="not($embed=true())">
	<span rel="tw:hasName">
	  <xsl:call-template name="get-theme-name">
	    <xsl:with-param name="theme" select="$node"/>
	  </xsl:call-template>
	  <xsl:if test="$admin=true()">
	    <a href="./edit">Edit</a>
	  </xsl:if>
	</span>
      </xsl:if>
      <xsl:variable name="logo-raw">
	<xsl:call-template name="get-theme-logo">
	  <xsl:with-param name="theme" select="$node"/>
	  <xsl:with-param name="root" select="$root"/>
	</xsl:call-template>
      </xsl:variable>
      <xsl:variable name="logo" select="exsl:node-set($logo-raw)"/>
      <xsl:call-template name="place-image">
	<xsl:with-param name="image" select="$logo/*"/>
	<xsl:with-param name="class">thumb</xsl:with-param>
      </xsl:call-template>
    </h1>
    <p class="lead">
      <xsl:variable name="prof-raw">
	<xsl:call-template name="get-theme-lead-prof">
	  <xsl:with-param name="theme" select="$node"/>
	  <xsl:with-param name="root" select="$root"/>
	</xsl:call-template>
      </xsl:variable>
      <xsl:variable name="prof" select="exsl:node-set($prof-raw)/*"/>
      <span class="label">Lead Professor: </span>
      <xsl:call-template name="place-person-link">
	<xsl:with-param name="person" select="$prof"/>
	<xsl:with-param name="rel">tw:hasLeadProfessor</xsl:with-param>
      </xsl:call-template>
<!--
      <a rel="tw:hasLeadProfessor" href="{$prof/@rdf:about}">
	<xsl:call-template name="get-person-name">
	  <xsl:with-param name="person" select="$prof"/>
	</xsl:call-template>
      </a>
-->
    </p>
    <p class="related">
      <xsl:variable name="concept-raw">
	<xsl:call-template name="get-theme-concepts">
	  <xsl:with-param name="theme" select="$node"/>
	  <xsl:with-param name="root" select="$root"/>
	</xsl:call-template>
      </xsl:variable>
      <xsl:variable name="concepts" select="exsl:node-set($concept-raw)"/>
      <span class="label">Related Concepts: </span>
      <xsl:for-each select="$concepts/*">
	<xsl:call-template name="place-concept-link">
	  <xsl:with-param name="concept" select="current()"/>
	  <xsl:with-param name="rel">tw:hasTopic</xsl:with-param>
	</xsl:call-template>
<!--
	<a href="{current()/@rdf:about}">
	<xsl:call-template name="get-concept-name">
	  <xsl:with-param name="concept" select="current()"/>
	</xsl:call-template>
	</a>
-->
	<xsl:if test="position()!=last()">
	  <xsl:text>, </xsl:text>
	</xsl:if>
      </xsl:for-each>
    </p>
    </div>
    </div>
  </xsl:template>
</xsl:stylesheet>
