<?xml version="1.0"?>
<!DOCTYPE xsl:stylesheet [
  <!ENTITY tw "http://tw.rpi.edu/schema/">
  <!ENTITY here "http://tw.rpi.edu/instances/">
]>
<xsl:stylesheet 
   version="1.0"
   xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
   xmlns:rdf="http://www.w3.org/1999/02/22-rdf-syntax-ns#"
   xmlns:rdfs="http://www.w3.org/2000/01/rdf-schema#"
   xmlns:tw="&tw;"
   xmlns:fn="http://www.w3.org/2005/xpath-functions"
   xmlns:time="http://www.w3.org/2006/time#"
   xmlns:owl="http://www.w3.org/2002/07/owl#"
   xmlns:foaf="http://xmlns.com/foaf/0.1/"
   xmlns:dc="http://purl.org/dc/terms/"
   xmlns="http://www.w3.org/1999/xhtml">

  <xsl:import href="/xslt/time-common.xsl"/>
  <xsl:import href="/xslt/project-common.xsl"/>
  <xsl:import href="/xslt/person-common.xsl"/>
     
  <xsl:template match="/">
    <link href="/css/announcements.css" rel="stylesheet" type="text/css"/>
    <table border="0" class="announcement-list">
      <xsl:for-each select="//tw:Announcement|//*[rdf:type/@rdf:resource='&tw;Announcement']">
	<xsl:sort select="tw:hasAnnouncementDate/*/time:inXSDDateTime" order="descending"/>
	<xsl:if test="position()&lt;5">
	<tr>
	  <td typeof="tw:Announcement">
	    <xsl:choose>
	      <xsl:when test="dc:source and foaf:depiction">
		<a href="{dc:source/@rdf:resource}" rel="dc:source">
		<xsl:if test="foaf:depiction">
		  <img class="left-align" style="width:100px">
		    <xsl:attribute name="src">
		      <xsl:value-of select="foaf:depiction/@rdf:resource|foaf:depiction/*/@rdf:about"/>
		    </xsl:attribute>
		    <xsl:attribute name="alt">
		      <xsl:value-of select="dc:title"/>
		    </xsl:attribute>
		  </img>
		</xsl:if>
		</a>
	      </xsl:when>
	      <xsl:otherwise>
		<xsl:if test="foaf:depiction">
		  <img class="left-align" style="width:100px">
		    <xsl:attribute name="src">
		      <xsl:value-of select="foaf:depiction/@rdf:resource|foaf:depiction/*/@rdf:about"/>
		    </xsl:attribute>
		    <xsl:attribute name="alt">
		      <xsl:value-of select="dc:title"/>
		    </xsl:attribute>
		  </img>
		</xsl:if>
	      </xsl:otherwise>
	    </xsl:choose>
	    <xsl:choose>
	      <xsl:when test="dc:source">
		<a href="{dc:source/@rdf:resource}" rel="dc:source">
		  <span property="dc:title">
		    <xsl:comment></xsl:comment>
		    <xsl:value-of select="dc:title"/>
		  </span>
		</a>
	      </xsl:when>
	      <xsl:otherwise>
		<span property="dc:title">
		  <xsl:comment></xsl:comment>
		  <xsl:value-of select="dc:title"/>
		</span>
	      </xsl:otherwise>
	    </xsl:choose>
	    <br/>
	    <span rel="tw:hasAnnouncementDate">
	      <xsl:apply-templates select="tw:hasAnnouncementDate/*"
				   mode="date"/>
	    </span><br/>
	    <div class="spacing">
	    <span property="dc:description">
	      <xsl:copy-of select="dc:description"/>
	    </span>
	    </div>
	    <xsl:if test="tw:hasProjectReference">
	      <span class="related">
	      <xsl:choose>
		<xsl:when test="count(tw:hasProjectReference)!=1">
		  <span>Related Projects: </span>
		</xsl:when>
		<xsl:otherwise>
		  <span>Related Project: </span>
		</xsl:otherwise>
	      </xsl:choose>
	      <xsl:for-each select="tw:hasProjectReference">
		<span rel="tw:hasProjectReference">
		  <xsl:apply-templates select="//*[@rdf:about=current()/@rdf:resource]|*" mode="link"/>
		</span>
		<xsl:if test="position()!=last()">
		  <xsl:text>, </xsl:text>
		</xsl:if>
	      </xsl:for-each>
	      </span>
	    </xsl:if>
	    <xsl:if test="tw:hasPersonReference">
	      <xsl:if test="count(tw:hasProjectReference)&gt;0">
		<br/>
	      </xsl:if>
	      <span class="related">
	      <xsl:choose>
		<xsl:when test="count(tw:hasPersonReference)!=1">
		  <span>Related People: </span>
		</xsl:when>
		<xsl:otherwise>
		  <span>Related Person: </span>
		</xsl:otherwise>
	      </xsl:choose>
	      <xsl:for-each select="tw:hasPersonReference">
		<span rel="tw:hasPersonReference">
		  <xsl:apply-templates select="//*[@rdf:about=current()/@rdf:resource]|*" mode="link"/>
		</span>
		<xsl:if test="position()!=last()">
		  <xsl:text>, </xsl:text>
		</xsl:if>
	      </xsl:for-each>
	      </span>
	    </xsl:if>
	  </td>
	</tr>
	</xsl:if>
      </xsl:for-each>
    </table>
  </xsl:template>
  
</xsl:stylesheet>


   
